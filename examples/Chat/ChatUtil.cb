
Const PORT              = 1337 // Porttinumero

//--------------------------------------------------------------------------------
// Vakioita joilla kerrotaan viestin tyyppi
//--------------------------------------------------------------------------------
Const NET_LOGIN         = 1     // Uusi client on liittynyt palvelimelle
Const NET_LOGOUT        = 2     // Client poistui palvelimelta
Const NET_LOGIN_OK      = 3     // Login onnistui
Const NET_LOGIN_FAILED  = 4     // Login epäonnistui
Const NET_CLIENT_INFO   = 5     // Clientille lähetetään tiedot kaikista muista clienteistä
Const NET_TEXT_MESSAGE  = 6     // Tekstiä tulossa
Const NET_IDLE          = 7     // Client lähettää vain tämän jos ei ole muuta lähetettävää
Const NET_END           = 255   // Viestin loppu

//--------------------------------------------------------------------------------
// Tässä includessa on funktioita joita käytetään sekä clientissä että serverissä
// mutta hieman eri tavoin. Muuttujassa gMode on tieto kummasta ohjelmasta on kyse.
//--------------------------------------------------------------------------------
Const MODE_CLIENT       = 1
Const MODE_SERVER       = 2
Global gMode

//--------------------------------------------------------------------------------
// Luodaan kokoelma clienteille
//--------------------------------------------------------------------------------
Type CLIENTS
    Field clientId      As String   // Clientin yksilöivä tunnus
    Field name          As String   // Nimimerkki
    Field lastActivity  As Integer  // Milloin viimeksi clientiltä on saatu dataa
EndType


//--------------------------------------------------------------------------------
// Kokoelma clienteille lähetettävistä viesteisä
//--------------------------------------------------------------------------------
Type MESSAGES
    Field clientId      As String   // Kenelle viesti on osoitettu
    Field msgType       As Byte     // Viestityyppi
    Field message       As String   // Viestin sisältö
    Field senderId      As String   // Lähettäjän id
    Field name          As String   // Viestin lähettäjän nimi
EndType

Global gFntNormal, gFntBold
gFntNormal  = LoadFont("Arial", 15)
gFntBold    = LoadFont("Arial", 15, ON)

//================================================================================
// Luodaan uusi client.
//================================================================================
Function CreateClient(_clientId$, _name$)
    // Lähetetään kaikille tieto uudesta clientistä vain jos tätä on kutsuttu SERVERISTÄ
    If gMode = MODE_SERVER Then
        For client.CLIENTS = Each CLIENTS
            msg.MESSAGES    = New(MESSAGES)
            msg\clientId    = client\clientId   // Kenelle tämä viesti lähetetään
            msg\msgType     = NET_LOGIN         // Viestin tyyppi
            msg\message     = _name             // Uuden clientin nimi
            msg\senderId    = _clientId         // Uuden clientin tunnus
        Next client
    EndIf
    // Tutkitaan vielä löytyykö clienttiä ennestään
    For client.CLIENTS = Each CLIENTS
        If client\clientId = _clientId Then
            // Löytyi
            client\name = _name
            client\lastActivity = Timer()
            Return True
        EndIf
    Next client

    // Luodaan uusi client
    client.CLIENTS      = New(CLIENTS)
    client\clientId     = _clientId
    client\name         = _name
    client\lastActivity = Timer()
EndFunction


//================================================================================
// Poistetaan client
//================================================================================
Function DeleteClient(_clientId$)
    // Etsitään client ja poistetaan se
    For client.CLIENTS = Each CLIENTS
        If client\clientId = _clientId Then
            // Client löytyi.
            // Lähetetään kaikille muille clienteille viesti poistumisesta. Jos SERVER
            If gMode = MODE_SERVER Then
                For cl.CLIENTS = Each CLIENTS
                    // Ei lähetetä tätä viestiä sille joka juuri poistui
                    If cl\clientId <> client\clientId Then
                        msg.MESSAGES    = New(MESSAGES)
                        msg\clientId    = cl\clientId       // Kenelle tämä viesti lähetetään
                        msg\msgType     = NET_LOGOUT        // Viestin tyyppi
                        msg\senderId    = client\clientId   // Kuka poistui
                    EndIf
                Next cl
                // Poistetaan kaikki viestit joita mahdollisesti on jonossa
                // poistettavalle clientille
                For msg.MESSAGES = Each MESSAGES
                    If msg\clientId = client\clientId Then Delete msg
                Next msg
            EndIf
            Delete client
            Exit
        EndIf
    Next client
EndFunction


//================================================================================
// Tulostetaan kaikki mukana olevat clientit ruudulle
//================================================================================
Function PrintClients()
    y = 10
    x = 10
    If gMode = MODE_CLIENT Then
        y = 25
        x = 510
    EndIf
    If gMode = MODE_SERVER Then
        SetFont gFntBold
        Text x, y, "Nimi"
        Text x + 100, y, "ID"
        y + 15
    EndIf
    
    SetFont gFntNormal
    For client.CLIENTS = Each CLIENTS
        Text x, y, client\name
        If gMode = MODE_SERVER Then
            Text x + 100, y, client\clientId
        EndIf
        y + 15
    Next client
EndFunction


//================================================================================
// Etsitään clientin nimi id:n perusteella
//================================================================================
Function GetClientName(_clientId$)
    For client.CLIENTS = Each CLIENTS
        If client\clientId = _clientId Then Return client\name
    Next client
    Return ""
EndFunction
